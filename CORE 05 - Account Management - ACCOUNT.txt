@@ DEPENDENCIES - Core

th u(NEWCOBJ,Account System <ACCOUNT>,account,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
@power [u(cobj,account)]=no_pay debit queue
th u(NEWCOBJ,Account Database <ACCOUNTS>,accounts,u(cobj,account),,,WIZARD SAFE,SIDEFX SAFE)

&IDLE_SECS [u(cobj,accounts)]=1800
&TIMEZONE [u(cobj,accounts)]=UTC

&ACONNECT`SUITE`MAIL [u(cobj,ancestor_player)]=@stop u(isguest,%@);@check u(op,MAIL);+mailcheck

&CMD`MAILCHECK [u(cobj,account)]=$+mailcheck:@attach %!/INC`CHECKPC=%#,1;@select/inline t(words(u(setr,ualts,u(FILTER,UMAIL,u(alts,%q<t1>)))))=1,{@pemit %0=ansi(hy,Unread Mail(s) by Character(s):) [iter(u(sortname,%q<ualts>),ansi(h,name(%i0)) ([u(umail,%i0)]),%b,\,%b)]},0,{@pemit %0=ansi(hy,You have no unread mail.)}
&FIL`UMAIL [u(cobj,ccp)]=t(u(umail,%0))

&SYSTEM`SWITCHES [u(cobj,account)]=setunion(setunion(setunion(u(SWITCHES`%q<sysname>`PLAYER),if(u(isadmin,%#),u(SWITCHES`%q<sysname>`ADMIN)),|,|),if(cor(u(OPEN_CREATION),u(conf,OPEN_ACCOUNT)),v(SWITCHES`%q<sysname>`OPEN_CREATION)),|,|),if(u(iswizard,%#),u(SWITCHES`%q<sysname>`WIZARD)),|,|)
&SYSTEM`NAME [u(cobj,account)]=u(strfirstof,%q<sysname>,ACCOUNT)

@@ CONFIG SECTION
&CONFIG`OPTIONS [u(cobj,account)]=AUTO_ACCOUNT|RECORD_LOGINS|CHANNELS_PLAYERS|OPEN_ACCOUNT|ACCOUNT_RENAME|WIZLIST_FOOTER|APPROVE_CHANNELS|UNAPPROVE_CHANNELS|WHO_HEADER|WHO_COLUMNS|WHERE_HEADER|WHERE_COLUMNS|GUEST_HOME|CHANNELS_GUESTS

&CONFIG`AUTO_ACCOUNT [u(cobj,account)]=Attempt to auto-bind accounts based on IP? Requires RECORD_LOGINS to be True.
&CONFIG`AUTO_ACCOUNT`DEFAULT [u(cobj,account)]=1
&CONFIG`AUTO_ACCOUNT`VALID [u(cobj,account)]=BOOL

&CONFIG`RECORD_LOGINS [u(cobj,account)]=Enable the login tracker. Records login times and IP addresses for all connections.
&CONFIG`RECORD_LOGINS`DEFAULT [u(cobj,account)]=1
&CONFIG`RECORD_LOGINS`VALID [u(cobj,account)]=BOOL

&CONFIG`CHANNELS_PLAYERS [u(cobj,account)]=Players will be added to these Channels on Create.
&CONFIG`CHANNELS_PLAYERS`DEFAULT [u(cobj,account)]=
&CONFIG`CHANNELS_PLAYERS`VALID [u(cobj,account)]=LIST

&CONFIG`OPEN_ACCOUNT [u(cobj,account)]=Players can create and manage their own accounts?
&CONFIG`OPEN_ACCOUNT`DEFAULT [u(cobj,account)]=1
&CONFIG`OPEN_ACCOUNT`VALID [u(cobj,account)]=BOOL

&CONFIG`ACCOUNT_RENAME [u(cobj,account)]=Players can rename their own accounts?
&CONFIG`ACCOUNT_RENAME`DEFAULT [u(cobj,account)]=1
&CONFIG`ACCOUNT_RENAME`VALID [u(cobj,account)]=BOOL

&CONFIG`WIZLIST_FOOTER [u(cobj,account)]=This message is displayed at the bottom of the +admin aka wizlist command.
&CONFIG`WIZLIST_FOOTER`DEFAULT [u(cobj,account)]=
&CONFIG`WIZLIST_FOOTER`VALID [u(cobj,account)]=WORD

&CONFIG`CHANNELS_GUESTS [u(cobj,account)]=Guests will be added to these Channels.
&CONFIG`CHANNELS_GUESTS`DEFAULT [u(cobj,account)]=
&CONFIG`CHANNELS_GUESTS`VALID [u(cobj,account)]=LIST

@@ ****** ACCOUNT SECTION *******
&CMD`+ACCOUNT`PENNMUSH [u(cobj,account)]=$^(?\:\+)?account(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ACCOUNT`MAIN
@set [u(cobj,account)]/CMD`+ACCOUNT`PENNMUSH=regexp
&CMD`+ACCOUNT`RHOSTMUSH [u(cobj,account)]=$^(?\:\+)?account(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ACCOUNT`MAIN
@set [u(cobj,account)]/CMD`+ACCOUNT`RHOSTMUSH=regexp
&CMD`+ACCOUNT`MAIN [u(cobj,account)]=th u(setq,sysname,ACCOUNT);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`ACCOUNT`[u(strfirstof,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,account)]/CMD`+ACCOUNT`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SWITCHES`ACCOUNT`PLAYER [u(cobj,account)]=EMAIL
&SWITCHES`ACCOUNT`ADMIN [u(cobj,account)]=BIND|UNBIND|UNBOUND|CREATE|LIST|RENAME|INACTIVE|CONFIG
&SWITCHES`ACCOUNT`WIZARD [u(cobj,account)]=ENABLE|DISABLE
&SWITCHES`ACCOUNT`OPEN_CREATION [u(cobj,account)]=REQUEST|ACCEPT|NEW|RENAME

&INC`ACCOUNT`CONFIG [u(cobj,account)]=@attach %!/INC`CONFIG

&INC`VALID`ACCOUNT [u(cobj,account)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: You must enter an Account name!;@check valid(name,u(setr,value,trim(stripansi(%0))))=@attach %!/INC`MSG=ERROR: Not a valid Account name! Try something simpler.;@stop cand(isdbref(u(setr,exist,u(find_in,u(cobj,accounts),%q<value>))),not(strmatch(num(%q<exist>),num(%1))))=@attach %!/INC`MSG=ERROR: Name is already in use.

&INC`ACCOUNT`FIND [u(cobj,account)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Account Name field empty.;@check strlen(u(setr,accid,namegraball(children(u(cobj,accounts)),%0)))=@attach %!/INC`MSG=ERROR: Account '%0' not found.;@stop gt(words(%q<accid>),1)=@attach %!/INC`MSG=ERROR: '%0' matched multiple accounts. Please be more specific.;th u(setq,accname,name(%q<accid>))

&INC`ACCOUNT`TARGET [u(cobj,account)]=@select/inline %0=#*,{@attach %!/INC`ACCOUNT`FIND=after(%0,#)},{@attach %!/INC`CHECKPC=%0,1;@check isdbref(%q<t1acc>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not bound to an account.;th u(setq,accid,%q<t1acc>);th u(setq,accname,name(%q<accid>))}

&INC`ACCOUNT`MAIN [u(cobj,account)]=@select/inline cand(u(isadmin,%#),strlen(%0))=1,{@attach %!/INC`ACCOUNT`TARGET},0,{@check isdbref(u(setr,accid,get(%#/D`ACCOUNT)))=@attach %!/INC`MSG=ERROR: No account to display.;th u(setq,accname,name(%q<accid>))};@attach %!/INC`ACCOUNT`SHOW

&INC`ACCOUNT`CREATE [u(cobj,account)]=@attach %!/INC`VALID`ACCOUNT=%0;@stop isdbref(u(find_in,u(cobj,accounts),%q<value>))=@attach %!/INC`MSG=ERROR: An account already exists with that name.;@check valid(name,%q<value>)=@attach %!/INC`MSG=ERROR: That name can't be used for an account.;@tel [u(setr,accid,objid(create(u(setr,accname,%q<value>))))]=u(cobj,accounts);@parent %q<accid>=u(cobj,accounts);th u(attrib_set,%q<accid>,D`ACCOUNT,%q<accid>);th u(attrib_set,%q<accid>,D`ACCOUNT`ID,u(call`2,volp_account,0,'[sqlescape(%q<accid>)]','[sqlescape(%q<accname>)]'));@attach %!/INC`MSG`CHAN={Created NEW ACCOUNT: %q<accname>}

&INC`ACCOUNT`NEW [u(cobj,account)]=@stop u(isguest,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;@stop strmatch(%#,#1)=@attach %!/INC`MSG=ERROR: #1 cannot be bound to an Account.;@stop isdbref(u(setr,accid,u(accid,%#,1)))=@attach %!/INC`MSG=ERROR: You are already bound to [name(%q<accid>)]!;@attach %!/INC`ACCOUNT`CREATE;@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%:;@attach %!/INC`MSG={You are now bound to ACCOUNT: %q<accname>!};@attach %!/INC`MSG`CHAN={Bound to NEW ACCOUNT: %q<accname> via +account/new}

&INC`ACCOUNT`LIST [u(cobj,account)]=@check t(u(setr,count,u(mysql,COUNT`ACTIVE_ACCOUNTS,0)))=@attach %!/INC`MSG=ERROR: There are no active accounts entered in the system.;@attach %!/INC`ACCOUNT`MAINLIST=Accounts,0

&INC`ACCOUNT`EMAIL [u(cobj,account)]=@select/inline u(conf,OPEN_CREATION)=1,{@check isdbref(u(setr,accid,u(accid,%#,1)))=@pemit %#=ERROR: This character is not bound to an Account.;@attach %!/INC`VALID`EMAIL=%0;th u(attrib_set`%va,%q<accid>,EMAIL,%q<value>);@attach %!/INC`MSG={You have set your Account email to: %q<value>}},{@check u(isadmin,%#)=@attach %!/INC`MSG={Only Staff may set emails!};@attach %!/INC`CHECKPC=%0,1;@check u(setr,accid,%q<t1acc>)=@pemit %#=ERROR: %q<t1name> is not bound to an Account.;@attach %!/INC`VALID`EMAIL=%1;th u(attrib_set,%q<accid>,EMAIL,%q<value>);@attach %!/INC`MSG={%q<t1name>'s Email has been set to: %q<value>};@attach %!/INC`MSG`NOTICE={Your Email was set to: %1},get(%q<accid>/CHARACTERS)}

&Q`COUNT`ACTIVE_ACCOUNTS [u(cobj,account)]=SELECT count(account_id) FROM volv_account_characters WHERE old_characters=? AND account_objid IS NOT NULL

&INC`ACCOUNT`INACTIVE [u(cobj,account)]=@check t(u(setr,count,u(mysql,COUNT`ACTIVE_ACCOUNTS,1)))=@attach %!/INC`MSG=ERROR: There are no inactive accounts to list.;@attach %!/INC`ACCOUNT`MAINLIST=Inactive Accounts,1

&INC`ACCOUNT`MAINLIST [u(cobj,account)]=@pemit %#=u(header,mudname() %0);@dolist lnum(1,u(setr,pages,add(div(%q<count>,30),1)))={th u(setq,accounts,u(mysql3,SELECT`ACCOUNTS,%1,mul(sub(u(setr,page,u(choosegame,%i0,%d0)),1),30)));@dolist/inline/delimit [chr(176)] %q<accounts>={th u(setq,data,u(choosegame,%i0,%d0));@pemit %#=u(subheader,name(u(setr,obj,elements(%q<data>,1,chr(177)))) - [if(u(setr,last,elements(%q<data>,6,chr(177))),elements(u(fancytime,%q<last>,%#),1 2),ansi(hx,N/A))][if(elements(%q<data>,4,chr(177)), - Disabled)]);@pemit %#=align(6 30 11 30,ansi(h,EMAIL:),get(%q<obj>/EMAIL),ansi(h,CHARACTERS:),iter(elements(%q<data>,5,chr(177)),u(pueblize,u(moniker,%i0),+finger [name(%i0)]),%b,\,%b))};@select/inline %q<page>=%q<pages>,{@pemit %#=u(footer,+account <id or player> for details)}}

&Q`SELECT`ACCOUNTS [u(cobj,account)]=SELECT account_objid,account_email,account_date_activity,account_disabled,character_objids FROM volv_account_characters WHERE old_characters=0 AND account_objid IS NOT NULL LIMIT 30 OFFSET ?

&FUN`ACCOUNT`CHARLINE [u(cobj,account)]=align(-6 3 -2 -3 -4 21 21 6,num(%0),switch(1,u(iswizard,%0),ansi(u(conf,WIZTAG),WIZ),hasflag(%0,ROYALTY),ansi(u(conf,ROYTAG),ROY),u(isadmin,%0),ansi(hy,ADM),haspower(%0,BUILDER),ansi(hm,BUI),u(isapproved,%0),ansi(hg,APP),ansi(hr,---)),default(%0/D`ALTS,0),u(getproperty,%0,MAJORABBR),default(%0/D`FINGER`TYPE,??),u(pueblize,u(moniker,%0),+finger [name(%0)]),if(get(%0/D`APPROVED),u(fancytime,get(%0/D`APPROVED),%#),ansi(hx,N/A)),u(lastidle,%0))

&INC`ACCOUNT`SHOW [u(cobj,account)]=th u(setq,pcs,u(sortname,children(%q<accid>)));@pemit %#=u(HEADER,ACCOUNT: [name(%q<accid>)]);@select/inline strlen(get(%q<accid>/EMAIL))=>0,{@pemit %#=u(separator,EMAIL: [get(%q<accid>/EMAIL)])};@pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(-6 3 2 3 4 21 21 6,DBREF,Sta,Al,Fac,Type,Name,Date Approved,LastOn));@pemit %#=u(SEPARATOR);@dolist/inline %q<pcs>={@pemit %#=u(FUN`ACCOUNT`CHARLINE,##)};@select/inline cand(u(player_config,%#,SYSTEM,OLD_ALTS),gt(words(u(setr,old,u(filter,OLDOBJID,get(%q<accid>/OLD),|,|)),|),0))=1,{@pemit %#=u(SEPARATOR,Old Characters);@pemit %#=align(-6 3 3 21 8 21,Dbref,,,Name,,Objid);@dolist/inline/delimit | [u(sortoldid,%q<old>,|,|)]={@pemit %#=align(-6 3 -3 21 8 21,before(##,:),,,after(##,~),,before(##,~))}};@pemit %#=u(footer)

&INC`ACCOUNT`REQUEST [u(cobj,account)]=@stop isdbref(u(setr,accid,u(accid,%#,1)))=@attach %!/INC`MSG=ERROR: You already belong to [name(%q<accid>)]!;@attach %!/INC`CHECKPC=%0,1;@check u(setr,reqacc,u(accid,%q<t1>,1))=@attach %!/INC`MSG=ERROR: %q<t1name> does not have an account!;@attach %!/INC`MSG=Request sent! Please use +request/accept %n on any character for that account to confirm.;th u(attrib_set,%q<reqacc>,REQUESTS,setunion(get(%q<reqacc>/REQUESTS),%:));@attach %!/INC`MSG=%n has requested to join this account. Use [u(pueblize,+account/accept %n)] to confirm.,u(alts,%q<t1>);@attach %!/INC`MSG`CHAN=Sent add request to [name(%q<reqacc>)].

&INC`ACCOUNT`ACCEPT [u(cobj,account)]=@check isdbref(u(setr,accid,u(accid,%#,1)))=@attach %!/INC`MSG=ERROR: You do not have an account!;@attach %!/INC`CHECKPC=%0,1;@stop u(setr,reqacc,u(accid,%q<t1>,1))=@attach %!/INC`MSG=ERROR: %q<t1name> already has an account!;@check match(get(%q<accid>/REQUESTS),%q<t1objid>)=@attach %!/INC`MSG=ERROR: No request pending from %q<t1name>.;th u(attrib_set,%q<accid>,REQUESTS,setdiff(get(%q<accid>/REQUESTS),%q<t1objid>));@attach %!/INC`MSG=%q<t2name> has been added to [name(%q<accid>)],u(alts,%#);@attach %!/INC`MSG`CHAN=Account request for %q<t1name> to [name(%q<accid>)] confirmed.;@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%q<t1objid>

&INC`ACCOUNT`BIND [u(cobj,account)]=@attach %!/INC`CHECKPC=%0,2;@stop strmatch(%q<t2>,#1)=@attach %!/INC`MSG=ERROR: #1 cannot be bound to an Account.;@select/inline %1=NEW,{@attach %!/INC`ACCOUNT`CREATE=%q<t2name>},{@attach %!/INC`ACCOUNT`TARGET=%1};@select/inline isdbref(%q<t2acc>)=1,{@attach %!/INC`ACCOUNT`REMFROMACCOUNT=%q<t2acc>,%q<t2objid>};@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%q<t2objid>;@attach %!/INC`MSG`NOTICE=You have been Account-Bound to [u(pueblize,%q<accname>,+account)].,%q<t2>;@attach %!/INC`MSG=You have Bound %q<t2name> to [u(pueblize,name(%q<accid>),+account [last(name(%q<accid>))])];@attach %!/INC`MSG`CHAN=Account-Bound %q<t2name> to [u(pueblize,%q<accname>,+account #%q<accname>)]

&INC`ACCOUNT`ADDTOACCOUNT [u(cobj,account)]=@parent %1=%0;@attach %!/INC`DOSQL=SET`ACCOUNT_ID,get(%1/D`ACCOUNT`ID),get(%1/D`ID);

&Q`SET`ACCOUNT_ID [u(cobj,account)]=UPDATE vol_character SET account_id=? WHERE character_id=?

&INC`ACCOUNT`REMFROMACCOUNT [u(cobj,account)]=@parent %1;@attach %!/INC`DOSQL=CLEAN`LOGIN_LOGS,get(%1/D`ID);@attach %!/INC`DOSQL=CLEAN`ACCOUNT_ID,get(%1/D`ID);@attach %!/INC`ACCOUNT`REMFROMACCOUNT`EXTRA

&Q`CLEAN`LOGIN_LOGS [u(cobj,account)]=DELETE FROM vol_login WHERE character_id=?
&Q`CLEAN`ACCOUNT_ID [u(cobj,account)]=UPDATE vol_character SET account_id=NULL WHERE character_id=?

&INC`ACCOUNT`UNBIND [u(cobj,account)]=@attach %!/INC`CHECKPC=%0,1;@check u(setr,accid,%q<t1acc>)=@attach %!/INC`MSG={ERROR: %q<t1name> is not a member of any accounts.};@attach %!/INC`ACCOUNT`REMFROMACCOUNT=%q<accid>,%q<t1objid>;@attach %!/INC`MSG={You have removed %q<t1name> from Account [name(%q<accid>)]};@attach %!/INC`MSG`NOTICE={You were removed from Account '[name(%q<accid>)]'},%q<t1>;@attach %!/INC`MSG`CHAN={Removed %q<t1name> from [name(%q<accid>)]}

&INC`ACCOUNT`UNBOUND [u(cobj,account)]=@check words(u(setr,unbound,switch(%va,PennMUSH,lsearch(all,type,player,elock,!D`ACCOUNT:?*),search(eplayer=!hasattr(##,D`ACCOUNT)))))=@attach %!/INC`MSG={There are no accountless PCs in the Database. Hooray!};@pemit %#=u(HEADER,Unbound Characters);@pemit %#=align(-6 3 3 27 25 9,Dbref,Sta,Alt,Name,IP,Last On);@pemit %#=u(SEPARATOR);@dolist/inline %q<unbound>={@pemit %#=u(FUN`ACCOUNT`CHARLINE,##)};@pemit %#=u(FOOTER)

&INC`ACCOUNT`RENAME [u(cobj,account)]=@select/inline cand(not(u(isadmin,%#)),cor(u(conf,OPEN_CREATION),u(conf,ACCOUNT_RENAME)))=1,{@check isdbref(u(setr,accid,u(accid,%#,1)))=@pemit %#=ERROR: This character is not bound to an Account.;@attach %!/INC`VALID`ACCOUNT=%0;@name %q<accid>=%q<value>;@attach %!/INC`MSG=Account name changed to: %q<value>.;@attach %!/INC`MSG`CHAN=Changed Account Name to: %q<value>},{@check u(isadmin,%#)=@attach %!/INC`MSG={Only Staff may rename accounts!};@attach %!/INC`ACCOUNT`TARGET=%0;@attach %!/INC`VALID`ACCOUNT=%1,%q<accid>;@name %q<accid>=%q<value>;@attach %!/INC`MSG=Account renamed to: %q<value>.;@attach %!/INC`MSG`CHAN=Renamed Account '%q<accname>' to: %q<value>}

&INC`ACCOUNT`DISABLE [u(cobj,account)]=@check strlen(%0)=@attach %!/INC`MSG={ERROR: Account ID empty.};@attach %!/INC`ACCOUNT`FIND=%0;@stop u(lmax,iter(u(setr,chars,get(%q<accid>/CHARACTERS)),u(isadmin,%i0)))=@attach %!/INC`MSG=ERROR: Cannot disable an account that contains an admin character. Demote them first.;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will disable Account %q<accname>. Bound characters will not be able to login. Are you sure? Enter the command again within ten seconds to continue.},ACCOUNT DISABLE %q<accid>;@attach %!/INC`MSG={[name(%q<accid>)] disabled!};@attach %!/INC`MSG`NOTICE={Your account was disabled.},u(setr,chars,get(%q<accid>/CHARACTERS));@attach %!/INC`MSG`CHAN={[name(%q<accid>)] disabled!};@attach %!/INC`DOSQL=ACCOUNT`TOGGLE,0,%q<accid>;th u(attrib_set,%q<accid>,D`DISABLED,1);@dolist/inline %q<chars>={@boot ##};

&Q`ACCOUNT`TOGGLE [u(cobj,account)]=UPDATE vol_account SET account_disabled=? WHERE account_id=?

&INC`ACCOUNT`ENABLE [u(cobj,account)]=@check strlen(%0)=@attach %!/INC`MSG={ERROR: Account ID empty.};@attach %!/INC`ACCOUNT`FIND=%0;@attach %!/INC`MSG={[name(%q<accid>)] enabled!};@attach %!/INC`MSG`NOTICE={Your account was enabled.},u(setr,chars,get(%q<accid>/CHARACTERS));@attach %!/INC`MSG`CHAN={[name(%q<accid>)] enabled!};@attach %!/INC`DOSQL=ACCOUNT`TOGGLE,1,%q<accid>;@attach %!/WIPE=%q<accid>,D`DISABLED

&PLAYER`CONNECT [u(cobj,account)]=@dolist/inline u(lattr,%!/PLAYER`CONNECT`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&PLAYER`CREATE [u(cobj,account)]=@dolist/inline u(lattr,%!/PLAYER`CREATE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&PLAYER`DISCONNECT [u(cobj,account)]=@dolist/inline u(lattr,%!/PLAYER`DISCONNECT`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}

&OBJECT`RENAME [u(cobj,account)]=@dolist/inline u(lattr,%!/OBJECT`RENAME`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`DESTROY [u(cobj,account)]=@dolist/inline u(lattr,%!/OBJECT`DESTROY`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`CREATE [u(cobj,account)]=@dolist/inline u(lattr,%!/OBJECT`CREATE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`MOVE [u(cobj,account)]=@dolist/inline u(lattr,%!/OBJECT`MOVE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}

&OBJECT`RENAME`ENTITY_NAME [u(cobj,account)]=u(mysql,ENTITY`RENAME,%1,%0)
&Q`ENTITY`RENAME [u(cobj,account)]=UPDATE vol_entity SET entity_name=? WHERE entity_objid=?

&PLAYER`CREATE`ID [u(cobj,account)]=th u(attrib_set,%0,D`ID,u(call`4,volp_character,0,'%0','[sqlescape(%1)]',u(csecs,%0),u(isguest,%0)));
&PLAYER`CONNECT`ID [u(cobj,account)]=th u(attrib_set,%0,D`ID,u(call`4,volp_character,0,'%0','[sqlescape(name(%0))]',u(csecs,%0),u(isguest,%0)));

&PLAYER`CONNECT`DISABLED [u(cobj,account)]=@select/inline cand(isobjid(u(setr,acc,u(accid,%0))),get(%q<acc>/D`DISABLED),not(u(isadmin,%0)))=1,{@attach %!/INC`MSG=Login denied! Your account is disabled.,%0;@boot %0}

&PLAYER`CONNECT`ACCOUNT [u(cobj,account)]=@stop cor(u(isguest,%0),strmatch(num(%0),#1));@attach %!/INC`CHECKPC=%0,1;@stop isdbref(%q<t1acc>)=&LAST %q<t1acc>=secs();@select/inline u(conf,AUTO_ACCOUNT)=1,{@select/inline words(u(setr,accid,trim(u(mysql,GET`ACCOUNTS,get(%0/LASTIP)))))=>1,{@attach %!/INC`MSG=Welcome to [mudname()]! Login records show multiple accounts for this site. Auto-Matching cannot continue. [if(u(open_creation),%BPlease use +account/request on an existing alt if you have one. Else\, you may +account/new to generate a new account.,%BPlease request an account from staff.)],%0},1,{@attach %!/INC`MSG=Welcome to [mudname()]! We found one account matching your login records. You have been auto-bound to [name(%q<accid>)]! Please contact staff if this is in error.,%0;@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%0},0,{@attach %!/INC`MSG=Welcome to [mudname()]! No account was found for this character.[if(u(open_creation),%BPlease use +account/request on an existing alt if you have one. Else\, you may +account/new to generate a new account.,%BPlease request an account from staff.)],%0}},0,{@attach %!/INC`MSG=Welcome to [mudname())]! No account was found for this character.[if(u(open_creation),%BPlease use +account/request on an existing alt if you have one. Else\, you may +account/new to generate a new account.,%BPlease request an account from staff.)],%0}

&Q`GET`ACCOUNTS [u(cobj,account)]=SELECT DISTINCT account_objid FROM volv_login WHERE character_is_guest=0 AND character_is_guest=0 AND ip_address=?

&PLAYER`DISCONNECT`ACCOUNT [u(cobj,account)]=@attach %!/INC`CHECKPC=%0,1;&LAST %q<t1acc>=secs();@attach %!/INC`DOSQL=UPDATE`ACCOUNT_LAST,%q<t1accid>;@attach %!/INC`DOSQL=UPDATE`CHARACTER_LAST,%q<t1id>

&Q`UPDATE`ACCOUNT_LAST [u(cobj,account)]=UPDATE vol_account SET account_date_activity=UTC_TIMESTAMP() WHERE account_id=?

&Q`UPDATE`CHARACTER_LAST [u(cobj,account)]=UPDATE vol_character SET character_date_activity=UTC_TIMESTAMP() WHERE character_id=?

&START`ACCOUNTCHECK [u(cobj,account)]=@trigger %!/LOOP`ACCOUNTCHECK

&LOOP`ACCOUNTCHECK [u(cobj,account)]=@dolist/inline/nobreak u(lwhoid)={@select/inline [isdbref(get(##/D`ACCOUNT))][strmatch(get(get(##/D`ACCOUNT)/EMAIL),*@*.*)]=0*,{@select/inline t(u(conf,OPEN_ACCOUNT))=1,{@attach %!/INC`MSG=[ansi(hr,WARNING:)] This Character has no Account! This is not supported. Please use [ansi(hw,+account/new <AccountName>)] to register an account. If you already have an account then use +account/request <alt> to begin the registration process.,##},0,{@attach %!/INC`MSG=[ansi(hr,WARNING:)] This Character has no Account! This is not supported. Please contact staff with your email or existing alt/account name to be bound to an account.}},10,{@select/inline t(u(conf,ACCOUNT_RENAME))=1,{@attach %!/INC`MSG=[ansi(hr,WARNING:)] Your Account does not follow the new email Guidelines. Please use [ansi(hw,+account/email <email>)] to fix this. Non-Email addresses may not be eligible for password resets or future database migration.},0,{@attach %!/INC`MSG=[ansi(hr,WARNING:)] Your Account does not follow new Email Guidelines. Please ask staff to give your account an Email Address. Without one you may not be eligible for password resets or future database migration.}}};@wait 1200=@trigger %!/LOOP`ACCOUNTCHECK

&PLAYER`CREATE`CHANNELS [u(cobj,account)]=@select/inline %va=PennMUSH,{@dolist/inline/delimit | [u(conf,CHANNELS_PLAYERS)]={@channel/on %i0=%0}},RhostMUSH,{}

&HLP`+ACCOUNT [u(cobj,account)]=The account system tracks players (and their alts) and their current status in the game (unapproved, approved, builder, admin, etc.).%R%R[ansi(hc,Player Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+account)] - Shows your account if you have one.%R[ansi(h,+account/new <name>)] - For new players. Enter this to start a new account on our game. You should preferably use your email address!%R[if(u(conf,OPEN_CREATION),[ansi(h,+account/rename <newname>)] - Renames your account.%R)][ansi(h,@password <old>=<new>)] - Change your password. If you forget your password\, staff will send a new one to your <email> on request - or if your Guest IP matches your previous logins.)]
+help/add Character/+account=[u(cobj,account)]/HLP`+ACCOUNT

&SHLP`+ACCOUNT [u(cobj,account)]=[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+account/list)] - Shows all accounts in the system.%R[ansi(h,+account/inactive)] - List inactive accounts.%R[ansi(h,+account <account>)] - Shows a specific account. Target can be a player\, OR an account name preceded by # such as #MushDude.%R[ansi(h,+account/create <name>)] - Create a new account.%R[ansi(h,+account/rename <account>=<newname>)] - Target as with +account. Renames account to <newname>.%R[ansi(h,+account/bind <player>=<account>)] - Binds <player> to an account\, removing them from any other account they're in. <account> can be the name of an character \(if they have an account\)\, the word NEW \(makes a new account using the character's name\)\, or the name of an account preceded by # - such as #MushDude.%R[ansi(h,+account/unbind <player>)] - Removes a player from any account they're in.%R[ansi(h,+account/unbound)] - Displays all unbound characters. Some are best left that way.%R[ansi(h,@newpass *<name>=<newpassword>)] - Set a new password for <name>. Note the asterisk\, this is necessary! This command is WIZARD only.)]
+shelp/add Character/+account=[u(cobj,account)]/SHLP`+ACCOUNT


@@ ****** ALTS SECTION *******

&CMD`+ALTS`PENNMUSH [u(cobj,account)]=$^(?\:\+)?alts(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ALTS`MAIN
@set [u(cobj,account)]/CMD`+ALTS`PENNMUSH=regexp
&CMD`+ALTS`RHOSTMUSH [u(cobj,account)]=$^(?\:\+)?alts(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ALTS`MAIN
@set [u(cobj,account)]/CMD`+ALTS`RHOSTMUSH=regexp
&CMD`+ALTS`MAIN [u(cobj,account)]=th u(setq,sysname,ALTS);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`ALTS`[u(strfirstof,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,account)]/CMD`+ALTS`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SWITCHES`ALTS`PLAYER [u(cobj,account)]=LIST|PRIORITY
&SWITCHES`ALTS`ADMIN [u(cobj,account)]=

&INC`ALTS`MAIN [u(cobj,account)]=@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1;@check get(%q<t1>/D`ALTS)=@attach %!/INC`MSG=[name(%q<t1>)] is not on any alts lists.;@attach %!/INC`MSG=Alts of [ansi(h,%q<t1name>)] ([u(hideidle,%q<t1>)]): [u(FUN`ALTS`LIST,%q<t1>)]

&INC`ALTS`LIST [u(cobj,account)]=@check u(setr,accid,u(accid,%#,1))=@attach %!/INC`MSG=ERROR: This character is not bound to an account.;@check strlen(%0)=@attach %!/INC`MSG=ERROR: List or target field empty. Please enter a name or a number to list this alt under.;@select/inline cand(strlen(%0),strlen(%1))=1,{@attach %!/INC`CHECKPC=%0,1;@check strmatch(u(accid,%q<t1objid>),%q<accid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not one of your registered characters.;@check cand(gte(u(setr,list,%1),0),isint(%1))=@attach %!/INC`MSG=ERROR: Alts list must be a whole number 0 or greater.},0,{@attach %!/INC`CHECKPC=%#,1;@check t(match(lnum(0,255),u(setr,list,%0)))=@attach %!/INC`MSG=ERROR: Alts list must be a whole number 0-255.};&D`ALTS %q<t1>=%q<list>;@attach %!/INC`DOSQL=SET`ALTS,%q<list>,%q<t1id>;@attach %!/INC`MSG=Okay! %q<t1name> will [if(%q<list>,now appear in Alts List #%q<list>.,not appear in any Alts lists.)]

&FUN`ALTS`LIST [u(cobj,account)]=if(get(%0/D`ALTS),u(strfirstof,u(itemize,iter(u(sortname,squish(iter(get(u(accid,%0)/CHARACTERS),if(eq(default(%0/D`ALTS,0),default(%i0/D`ALTS,0)),%i0)))),u(moniker,%i0)[if(not(%1),\([u(hideidle,%i0)]\))],%b,|),|,and,\,),None),None)

&Q`SET`ALTS [u(cobj,account)]=UPDATE vol_character SET character_alt=? WHERE character_id=?

&INC`ALTS`PRIORITY [u(cobj,account)]=@check u(setr,accid,u(accid,%#,1))=@attach %!/INC`MSG=ERROR: This character is not bound to an account.;@select/inline t(strlen(%0))=1,{@select/inline cand(strlen(%0),strlen(%1))=1,{@attach %!/INC`CHECKPC=%0,1;@check strmatch(u(accid,%q<t1objid>),%q<accid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not one of your registered characters.;@check cand(gte(u(setr,list,%1),0),isint(%1))=@attach %!/INC`MSG=ERROR: Alts priority must be a whole number 0 or greater.},0,{@attach %!/INC`CHECKPC=%#,1;@check cand(gte(u(setr,list,%0),0),isint(%0))=@attach %!/INC`MSG=ERROR: Alts priority must be a whole number 0 or greater.};&D`ALTS`PRIORITY %q<t1>=%q<list>;@attach %!/INC`MSG=Okay! %q<t1name>'s Priority is now: %q<list>},0,{@attach %!/INC`ALTS`PRIORITY`LIST}

&INC`ALTS`PRIORITY`LIST [u(cobj,account)]=@attach %!/INC`MSG=Your Alts by Priority: [iter(revwords(u(sortpriority,u(alts,%#))),u(moniker,%i0) ([default(%i0/D`ALTS`PRIORITY,0)]),%b,\,%b)]

&HLP`+ALTS [u(cobj,account)]=The Alts system allows you to specify what characters of yours other players can associate with you. It also allows you to configure which alts take precedent (if any) for receiving messages from channels and BBS notifications.%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+alts <target>)] - Check someone's alts. Alts are any characters with the same ALT ID. Without a target it will use yourself\, showing you what others would see.%R[ansi(h,+alts/list <#>)] or [ansi(h,+alts/list <alt>=<#>)] - Set your current character or an alt of yours to use a specific ALT ID. Characters with the same ALT ID are linked on +alts displays. Alt IDs are numbers.%R%RAll Alts begin with Alt ID 0\, which is special. Characters with Alt ID 0 have no alts period.%R%R[ansi(h,+alts/priority <#>)] or [ansi(h,+alts/priority <alt>=<#>)] - Set your Alt PRIORITY.%R[ansi(h,+alts/priority)] - Show the priority settings of all your alts.%R%RThe PRIORITY affects channel reception. In both cases\, the HIGHEST PRIORITY ALT who is eligible to receive the message \(has the channel turned on and not gagged\) will receive it. Where multiple alts have the same priority\, all with that priority will receive it.%R%RFor example\, if Alts A and B are set to Priority 1 and have the Public channel on\, and Alt C also has it on but is priority 0\, only A and B will see messages from the Public channel. If A and B gag or turn it off\, C will once again receive Public messages.)]
+help/add Character/+alts=[u(cobj,account)]/HLP`+ALTS


@@ ****** APPROVAL SECTION *********
&CMD`+APPROVE`PENNMUSH [u(cobj,account)]=$^(?\:\+)?(approve|unapprove)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+APPROVE`MAIN
@set [u(cobj,account)]/CMD`+APPROVE`PENNMUSH=regexp
&CMD`+APPROVE`RHOSTMUSH [u(cobj,account)]=$^(?\:\+)?(approve|unapprove)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+APPROVE`MAIN
@set [u(cobj,account)]/CMD`+APPROVE`RHOSTMUSH=regexp
&CMD`+APPROVE`MAIN [u(cobj,account)]=th u(setq,sysname,APPROVE);@attach %!/INC`GETSWITCH=%2;@attach %!/INC`%1`[u(strfirstof,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,account)]/CMD`+APPROVE`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SWITCHES`APPROVE`PLAYER [u(cobj,account)]=LIST
&SWITCHES`APPROVE`ADMIN [u(cobj,account)]=

&INC`APPROVE`MAIN [u(cobj,account)]=@select/inline cand(u(isadmin,%#),strlen(%0))=1,{@attach %!/INC`CHECKPC=%0,1;@stop u(isapproved,%q<t1>)=@attach %!/INC`MSG=ERROR: They are already approved!;@attach %!/INC`APPROVE`DO=%q<t1>,%#,%1;@attach %!/INC`MSG=You have approved %q<t1name> for play!;@attach %!/INC`MSG`CHAN=%q<t1name> approved for play!},{@attach %!/INC`MSG=You [if(u(isapproved,%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

&INC`APPROVE`DO [u(cobj,account)]=@check u(setr,results,u(call`4,volp_approve,0,get(%0/D`ID),get(%1/D`ID),1,'[sqlescape(%2)]'))=@attach %!/INC`MSG=ERROR: Could not run approval. reason: %q<results>;th u(attrib_set,%0,D`APPROVED,secs());th u(attrib_set,%0,V`APPROVED,secs());@select/inline words(u(setr,channels,u(conf,APPROVE_CHANNELS)))=>0,{@attach %!/INC`MSG`CHAN=[ansi(h,u(moniker,%0))] is now approved for play![if(strlen(%2),%b%2)],u(conf,APPROVE_CHANNELS),,1,,1}

&Q`SET`APPROVE [u(cobj,account)]=UPDATE vol_character SET character_date_approved=UTC_TIMESTAMP(),character_is_approved=1
&Q`SET`UNAPPROVE [u(cobj,account)]=UPDATE vol_character SET character_date_approved=NULL,character_is_approved=0

&INC`UNAPPROVE`DO [u(cobj,account)]=@check u(setr,results,u(call`4,volp_approve,0,get(%0/D`ID),get(%1/D`ID),0,'[sqlescape(%2)]'))=@attach %!/INC`MSG=ERROR: Could not run un-approval. reason: %q<results>;th u(attrib_set,%0,D`APPROVED,0);th u(attrib_set,%0,V`APPROVED,0);@select/inline words(u(setr,channels,u(conf,UNAPPROVE_CHANNELS)),|)=>0,{@attach %!/INC`MSG`CHAN=[ansi(h,u(moniker,%0))] is no longer approved for play![if(strlen(%2),%b%2)],%q<channels>,,1,,1}

&INC`UNAPPROVE`MAIN [u(cobj,account)]=@select/inline u(isadmin,%#)=1,{@attach %!/INC`CHECKPC=%0,1;@check u(isapproved,%q<t1>)=@attach %!/INC`MSG=ERROR: They are not approved!;@attach %!/INC`UNAPPROVE`DO=%q<t1>,%#,%1;@attach %!/INC`MSG=You have unapproved %q<t1name> for play!;@attach %!/INC`MSG`CHAN=%q<t1name> unapproved for play.;@attach %!/INC`MSG`PUBCHAN=%q<t1name> unapproved for play.},{@attach %!/INC`MSG=You [if(u(isapproved,%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

&CONFIG`APPROVE_CHANNELS [u(cobj,account)]=Channels to announce Approvals (and optional blurb message) on.
&CONFIG`APPROVE_CHANNELS`DEFAULT [u(cobj,account)]=
&CONFIG`APPROVE_CHANNELS`VALID [u(cobj,account)]=LIST

&CONFIG`UNAPPROVE_CHANNELS [u(cobj,account)]=Channels to announce un-Approvals (and optional blurb message) on.
&CONFIG`UNAPPROVE_CHANNELS`DEFAULT [u(cobj,account)]=
&CONFIG`UNAPPROVE_CHANNELS`VALID [u(cobj,account)]=LIST

&SHLP`+APPROVE [u(cobj,account)]=[ansi(h,+approve <name>)]%RThis will approve <name>%R%R[ansi(h,+unapprove <name>)]%RThis will unapprove <name>%R%RStaff are always considered 'approved' but may need to have the UNREGISTERED flag removed with @set if they intend to use @powers.
+shelp/add Character/+approve=[u(cobj,account)]/SHLP`+APPROVE

@@ ******** ID SECTION *******
&CMD`+ID`PENNMUSH [u(cobj,account)]=$^(?\:\+)?(id)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ID`MAIN
@set [u(cobj,account)]/CMD`+ID`PENNMUSH=regexp
&CMD`+ID`RHOSTMUSH [u(cobj,account)]=$^(?\:\+)?(id)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ID`MAIN
@set [u(cobj,account)]/CMD`+ID`RHOSTMUSH=regexp
&CMD`+ID`MAIN [u(cobj,account)]=th u(setq,sysname,ID);@attach %!/INC`GETSWITCH=%2;@attach %!/INC`ID`[u(strfirstof,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,account)]/CMD`+ID`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SWITCHES`ID`ADMIN [u(cobj,account)]=SEARCH|MERGE

&INC`ID`MAIN [u(cobj,account)]=@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1;@attach %!/INC`MSG=The Database ID for %q<t1name> is: %q<t1id>

&INC`ID`SEARCH [u(cobj,account)]=@pemit %#=u(header,Database IDs for Characters Like: %0);@pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(1 6 30 36,D,ID,OBJID,Name));@pemit %#=u(separator);@dolist/inline/delimit [chr(176)] [u(mysql3,SEARCH`CHARACTERS,sqlescape(%0))]={th u(setq,data,u(choosegame,%i0,%d0));@pemit %#=align(1 6 30 36,if(elements(%q<data>,1,chr(177)),ansi(hr,X),%b),elements(%q<data>,2,chr(177)),elements(%q<data>,3,chr(177)),elements(%q<data>,4,chr(177)))};@pemit %#=u(footer)

&Q`SEARCH`CHARACTERS [u(cobj,account)]=SELECT character_is_deleted,character_id,character_objid,character_name FROM volv_character WHERE character_name LIKE '%!%' ORDER BY character_name

&INC`ID`MERGE [u(cobj,account)]=@attach %!/INC`CHECKPC=%0,1;@check u(setr,oldid,u(mysql,EXACT`CHARACTER_ID,%1))=@attach %!/INC`MSG=ERROR: Could not find ID '%1';@attach %!/INC`VERIFY=[ansi(hr,WARNING:)] This will merge %q<t1name> into the Old Character ID '%1'. All scenes\, jobs\, and board posts of Character ID %q<t1id> will be assigned to the old ID. Other data associated with ID %q<t1id> will be lost. Is this okay? Enter the same command again in ten seconds to continue.,MERGE %q<t1> %q<oldid>;@dolist/inline JLINK BB BB2 PLOT ACTOR={@attach %!/INC`DOSQL=MERGE`##,%q<oldid>,%q<t1id>};@attach %!/INC`DOSQL=MERGE`DELETE_OLD,%q<t1id>;@attach %!/INC`DOSQL=MERGE`RESURRECT,%q<oldid>;@attach %!/INC`DOSQL=MERGE`OBJID,%q<t1objid>,%q<oldid>;th u(attrib_set,%q<t1>,D`ID,%q<oldid>);@attach %!/INC`MSG`CHAN=Re-Assigned %q<t1name> to the Old ID: %q<oldid>;@attach %!/INC`MSG`NOTICE=You were re-assigned the old character id: %q<oldid>,%q<t1>;@attach %!/INC`MSG=Reassignment complete.

&Q`EXACT`CHARACTER_ID [u(cobj,account)]=SELECT character_id FROM volv_character WHERE character_id=? LIMIT 1

&Q`MERGE`DELETE_OLD [u(cobj,account)]=DELETE from vol_entity WHERE entity_id=?
&Q`MERGE`OBJID [u(cobj,account)]=UPDATE vol_entity SET entity_objid=? WHERE entity_id=?
&Q`MERGE`RESURRECT [u(cobj,account)]=UPDATE vol_character SET character_is_deleted=0 WHERe character_id=?

&Q`MERGE`JLINK [u(cobj,account)]=UPDATE vol_jlink SET character_id=? WHERE character_id=?
&Q`MERGE`BB [u(cobj,account)]=UPDATE vol_bbpost SET entity_id=? WHERE entity_id=?
&Q`MERGE`BB2 [u(cobj,account)]=UPDATE vol_bbcomment SET entity_id=? WHERE entity_id=?
&Q`MERGE`PLOT [u(cobj,account)]=UPDATE vol_runner SET character_id=? WHERE entity_id=?
&Q`MERGE`ACTOR [u(cobj,account)]=UPDATE vol_actor SET character_id=? WHERE entity_id=?

&PLAYER`CREATE`MATCH [u(cobj,account)]=@check words(u(setr,results,u(mysql3,SEARCH`CHARACTERS_NEW,sqlescape(%1))),chr(176))={@attach %!/INC`MSG`CHAN=[ansi(hr,ALERT:)] Character '%1' (%0) Created! If this is a re-created old character\, check +shelp +id about re-assigning their old ID.;@select/inline isdbref(%#)=1,{@attach %!/INC`MSG=[ansi(hr,ALERT:)] Character '%1' (%0) Created! If this is a re-created old character\, check +shelp +id about re-assigning their old ID.,%#}};@attach %!/INC`MSG`CHAN=[ansi(hr,ALERT:)] Character '%1' (%0) Created! Possible re-creation of: [iter(%q<results>,elements(%i0,3,chr(177)) - \([elements(%i0,1,chr(177))]\, [elements(%i0,2,chr(177))]\),chr(176),\,%b)]. Check +shelp +id for info about re-assigning old ids.;@select/inline isdbref(%#)=1,{@attach %!/INC`MSG=[ansi(hr,ALERT:)] Character '%1' (%0) Created! Possible re-creation of: [iter(%q<results>,elements(%i0,3,chr(177)) - \([elements(%i0,1,chr(177))]\, [elements(%i0,2,chr(177))]\),chr(176),\,%b)]. Check +shelp +id for info about re-assigning old ids.,%#}

&Q`SEARCH`CHARACTERS_NEW [u(cobj,account)]=SELECT character_id,character_objid,character_name FROM volv_character WHERE character_name LIKE '%!%' AND character_is_deleted=1

