th u(NEWCOBJ,Guest Management <GUEST>,login,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&PLAYER`DISCONNECT`FIXNAME [u(cobj,guest)]=@stop %1;@check u(isguest,%0);@attach %!/INC`MSG`CHAN=[ansi(h,name(%0))] disconnected.,u(game_config,CHANNELS,JOIN_GUESTS),,1,,1;@select/inline hasattr(%0/D`OLDNAME)=1,{@name %0=[get(%0/D`OLDNAME)];@attach %!/WIPE=%0,D`OLDNAME}

&PLAYER`DISCONNECT`TELGUEST [u(cobj,guest)]=@stop %1;@select/inline u(isguest,%0)=1,{@select/inline %va=PennMUSH,{@tel/silent %0=u(game_config,NAVIGATION,GUEST_START,#0)},RhostMUSH,{@tel/quiet %0=u(game_config,NAVIGATION,GUEST_START,#0)}}

&PLAYER`CONNECT`GUEST [u(cobj,guest)]=@check u(isguest,%0);th u(setq,chan,first(u(setr,chans,u(game_config,CHANNELS,JOIN_GUESTS)),|));@wait 4=@attach %!/INC`MSG=Welcome to [mudname()]\, [name(%0)]! You can speak on the %q<chan> channel with [ansi(h,+[left(%q<chan>,1)] <text here>)]. You can give yourself a custom Guest name by typing [ansi(h,+myname <word>)] \(Guest will be appended after <word>\). You can find our website at [u(weblink,mudurl())],%0;@attach %!/INC`MSG`CHAN=[ansi(h,name(%0))] connected to [mudname()]!,%q<chans>,,1,,1

&CMD`+MYNAME [u(cobj,guest)]=$+myname *:@check u(isguest,%#)=@attach %!/INC`MSG=ERROR: Permission denied. Guest only!;@check valid(playername,%0 Guest)=@attach %!/INC`MSG=Sorry\, you may not rename yourself to that. Try something simpler.;@select/inline hasattr(%#/D`OLDNAME)=0,{&D`OLDNAME %#=[name(%#)]};@attach %!/INC`MSG`CHAN=[ansi(h,%n)] renamed to %0 Guest,u(game_config,CHANNELS,JOIN_GUESTS),,1,,1;@name %#=%0 Guest;@attach %!/INC`MSG=You are now known as '%0 Guest'.

&CMD`+MAKEGUEST [u(cobj,guest)]=$+makeguest *:@check u(iswizard,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;@check valid(playername,%0)=@attach %!/INC`MSG=ERROR: That is not a valid character name.;@pcreate %0=guest;@attach %!/INC`CHECKPC=%0,1;@describe %q<t1>=This is a Guest! Be kind to them!;@dolist/inline/delimit | [u(game_config,channels,join_guests)]={@chan/on %i0=%q<t1>};@tel %q<t1>=[u(game_config,navigation,guest_start)];@select/inline %va=PennMUSH,{@set %q<t1>=!UNREGISTERED;@power %q<t1>=GUEST},RhostMUSH,{@set %q<t1>=GUEST;th u(setq,guests,search(EPLAYER=\[hasflag(##,GUEST)\]));@admin guest_namelist=!ALL %q<guests>};@attach %!/INC`MSG=Guest '%0' created!;@attach %!/INC`MSG`CHAN=Guest '%0' created!

&CMD`+LISTGUESTS [u(cobj,guest)]=$+listguests:@check words(u(setr,guests,u(choosegame,lsearch(all,type,player,elock,POWER^GUEST),search(FLAGS=!))))=@attach %!/INC`MSG=ERROR: No Guests in the system.;@pemit %#=u(header,mudname() Guests);@pemit %#=ansi(u(color,%#,COLOR,COLUMN_NAMES),align(7 18 26 24,DBREF,Name,Current Name,Alias));@pemit %#=u(separator);@dolist/inline %q<guests>={@pemit %#=align(7 18 26 24,##,default(##/D`OLDNAME,name(##)),name(##),u(fullalias,##))};@pemit %#=u(subheader)

@select/inline %va=RhostMUSH,{th u(setq,guests,search(EPLAYER=\[hasflag(##,GUEST)\]));@admin guest_namelist=!ALL %q<guests>}

&SHLP`GUESTS [u(cobj,guest)]=Guests are special characters that exist solely to allow new players to logon without creating a character. They're traditionally restricted to a single channel (typically a Guest channel, sometimes the public channel) and have no privileges to modify themselves or anything else.%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+makeguest <name>)] - Creates a new guest. The NAVIGATION/GUEST_START and CHANNELS/JOIN_GUESTS +gameconfig options will be used and it will be given a basic @desc.%R[ansi(h,+listguests)] - Used to display existing guests and relevant info.)]
+shelp/add Administration/Guests=[u(cobj,guest)]/SHLP`GUESTS