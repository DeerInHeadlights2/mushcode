@@ DEPENDENCIES: Core

th u(NEWCOBJ,Finger <FINGER>,finger,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+FINGER`PENNMUSH [u(cobj,finger)]=$^(?s)(?\:\+)?finger(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*?))?$:@attach %!/CMD`+FINGER`MAIN
@set [u(cobj,finger)]/CMD`+FINGER`PENNMUSH=regexp
&CMD`+FINGER`RHOSTMUSH [u(cobj,finger)]=$^(?s)(?\:\+)?finger(?\:/(\\S+))?(?\: +(.+?))?(?\:=(.*?))?$:@attach %!/CMD`+FINGER`MAIN
@set [u(cobj,finger)]/CMD`+FINGER`RHOSTMUSH=regexp
&CMD`+FINGER`MAIN [u(cobj,finger)]=@attach %!/INC`GETSWITCH=%1;@include %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,finger)]/CMD`+FINGER`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SYSTEM`NAME [u(cobj,finger)]=FINGER

&SWITCHES`PLAYER [u(cobj,finger)]=SET|GET
&SWITCHES`ADMIN [u(cobj,finger)]=CONFIG

&INC`MAIN [u(cobj,finger)]=@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1;@attach %!/FINGER`[u(conf,FORMATTER)]

&FINGER`STANDARD [u(Cobj,finger)]=th u(setr,co,u(conf,FIELD_COLOR));@pemit %#=u(HEADER,if(strlen(u(setr,hdr,u(conf,HEADER_CONTENTS))),u(strfirstof,u(getproperty,%q<t1>,%q<hdr>),get(%q<t1>/D`FINGER`%q<hdr>))),,,1);th u(setq,col1,u(conf,LEFT_COLUMN));th u(setq,rjust1,lmath(max,iter(%q<col1>,strlen(elements(##,2,~)),|,%b)));th u(setq,col2,u(conf,RIGHT_COLUMN));th u(setq,rjust2,lmath(max,iter(%q<col2>,strlen(elements(##,2,~)),|,%b)));@pemit %#=align(1. [floor(fdiv(sub(u(width,%#),7),2))] 1. [ceil(fdiv(sub(u(width,%#),7),2))] 1.,ansi(u(color,%#,BORDER),|),iter(%q<col1>,ansi(%q<co>,rjust(elements(##,2,~),%q<rjust1>)):%B[u(strfirstof,u(getproperty,%q<t1>,elements(##,1,~)),get(%q<t1>/D`FINGER`[elements(##,1,~)]),elements(##,3,~))],|,%R),ansi(u(color,%#,BORDER),|),iter(%q<col2>,ansi(%q<co>,rjust(elements(##,2,~),%q<rjust2>)):%B[u(strfirstof,u(getproperty,%q<t1>,elements(##,1,~)),get(%q<t1>/D`FINGER`[elements(##,1,~)]),elements(##,3,~))],|,%R),ansi(u(color,%#,BORDER),|));@dolist/inline/delimit | [u(conf,BOTTOM_COLUMN)]=@select/inline strlen(u(setr,found,u(strfirstof,u(getproperty,%q<t1>,elements(##,1,~)),get(%q<t1>/D`FINGER`[elements(##,1,~)]),elements(##,3,~))))=>0,{@pemit %#=u(SEPARATOR,u(capnames,u(strfirstof,elements(##,2,~),##)),,,1);@pemit %#=align(1. [sub(u(width,%#),4)] 1.,ansi(u(color,%#,BORDER),|),edit(%q<found>,%t,space(8)),ansi(u(color,%#,BORDER),|))};@pemit %#=u(SUBHEADER,if(strlen(u(setr,shdr,u(conf,FOOTER_CONTENTS))),u(strfirstof,u(getproperty,%q<t1>,%q<shdr>),get(%q<t1>/D`FINGER`%q<shdr>))),,,1)

&INC`SET [u(cobj,finger)]=@attach %!/INC`CHECKPC=if(cand(u(isadmin,%#),strmatch(%0,*/*)),before(%0,/),%#),1;@attach %!/INC`PARTIAL=if(cand(u(isadmin,%#),strmatch(%0,*/*)),after(%0,/),%0),setunion(setunion(u(conf,PLAYER_FIELDS),if(u(isadmin,%#),u(conf,ADMIN_FIELDS)),|,|),if(u(approved,%#),,u(conf,CHARGEN_FIELDS)),|,|),|,field;@select/inline strlen(%1)=0,{@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] Setting %q<field> to nothing will clear it. Are you sure? Enter that again to verify!},CLEAR FINGER %q<field>},>0,{@attach %!/INC`CHECK`%q<field>=%1};&D`FINGER`%q<field> %q<t1>=%1;@attach %!/INC`MSG=You set [if(strmatch(%#,%q<t1>),your,%q<t1name>'s)] [u(capnames,%q<field>)] Finger Field to: %1;@select/inline strmatch(%#,%q<t1>)=0,{@attach %!/INC`MSG`NOTICE=Your [u(capnames,%q<field>)] Finger Field is now: %1,%q<t1>}

&INC`GET [u(cobj,finger)]=@attach %!/INC`CHECKPC=if(strmatch(%0,*/*),before(%0,/),%#),1;@attach %!/INC`PARTIAL=if(strmatch(%0,*/*),after(%0,/),%0),setunion(setunion(u(conf,PLAYER_FIELDS),u(conf,ADMIN_FIELDS),|,|),u(conf,CHARGEN_FIELDS),|,|),|,field;@pemit %#=u(strfirstof,u(getproperty,%q<T1>,%q<field>),get(%q<t1>/D`FINGER`%q<field>))

&CONFIG`OPTIONS [u(cobj,finger)]=FORMATTER|HEADER_CONTENTS|FOOTER_CONTENTS|LEFT_COLUMN|RIGHT_COLUMN|BOTTOM_COLUMN|PLAYER_FIELDS|ADMIN_FIELDS|CHARGEN_FIELDS|FIELD_COLOR

&CONFIG`FORMATTER [u(cobj,finger)]=+finger calls FINGER`<this> to render the display. Leave this be unless you know how to MUSHcode an alternate renderer!
&CONFIG`FORMATTER`DEFAULT [u(cobj,finger)]=STANDARD
&CONFIG`FORMATTER`VALID [u(cobj,finger)]=WORD

&CONFIG`HEADER_CONTENTS [u(cobj,finger)]=Header Text for +finger. See +shelp +finger
&CONFIG`HEADER_CONTENTS`DEFAULT [u(cobj,finger)]=ALIASMONIKER
&CONFIG`HEADER_CONTENTS`VALID [u(cobj,finger)]=WORD

&CONFIG`FOOTER_CONTENTS [u(cobj,finger)]=Text for +finger's footer. See +shelp +finger
&CONFIG`FOOTER_CONTENTS`DEFAULT [u(cobj,finger)]=
&CONFIG`FOOTER_CONTENTS`VALID [u(cobj,finger)]=WORD

&CONFIG`LEFT_COLUMN [u(cobj,finger)]=Finger definition for Left Column. See +shelp +finger
&CONFIG`LEFT_COLUMN`DEFAULT [u(cobj,finger)]=SEX~Sex|SPECIES~Species
&CONFIG`LEFT_COLUMN`VALID [u(cobj,finger)]=LIST

&CONFIG`RIGHT_COLUMN [u(cobj,finger)]=Finger definition for Right Column. See +shelp +finger
&CONFIG`RIGHT_COLUMN`DEFAULT [u(cobj,finger)]=JOB~Job
&CONFIG`RIGHT_COLUMN`VALID [u(cobj,finger)]=LIST

&CONFIG`BOTTOM_COLUMN [u(cobj,finger)]=Finger definition for vertical column. Below the right/left split. See +shelp +finger.
&CONFIG`BOTTOM_COLUMN`DEFAULT [u(cobj,finger)]=GROUPNAMES2~Groups|QUOTE~Quote|PROFILE~Profile|SKILLS~Skills|WIKI~Wiki
&CONFIG`BOTTOM_COLUMN`VALID [u(cobj,finger)]=LIST

&CONFIG`PLAYER_FIELDS [u(Cobj,finger)]=Fields settable by Players at any time.
&CONFIG`PLAYER_FIELDS`DEFAULT [u(cobj,finger)]=JOB|SPECIES|QUOTE|PROFILE|SKILLS
&CONFIG`PLAYER_FIELDS`VALID [u(cobj,finger)]=LIST

&CONFIG`ADMIN_FIELDS [u(cobj,finger)]=Fields settably by Admin only.
&CONFIG`ADMIN_FIELDS`DEFAULT [u(cobj,finger)]=
&CONFIG`ADMIN_FIELDS`VALID [u(cobj,finger)]=LIST

&CONFIG`CHARGEN_FIELDS [u(cobj,finger)]=Fields settable by players but only while unapproved/in chargen.
&CONFIG`CHARGEN_FIELDS`DEFAULT [u(cobj,finger)]=
&CONFIG`CHARGEN_FIELDS`VALID [u(cobj,finger)]=LIST

&CONFIG`FIELD_COLOR [u(cobj,finger)]=Color for field names. Such as Group: or Sex: or Theme:
&CONFIG`FIELD_COLOR`DEFAULT [u(cobj,finger)]=hw
&CONFIG`FIELD_COLOR`VALID [u(cobj,finger)]=COLOR

@@ HELP SECTION
&HLP`FINGER [u(cobj,finger)]=The +finger system keeps track of and presents in-character information for some community flavor. By reading someone's finger, you can gain a brief overview of what the character is about!%R%R[ansi(hc,Commands)]%R[align(5 72,,[ansi(h,+finger)] - Show your own finger.%R[ansi(h,+finger <name>)] - Show someone else's +finger.%R[ansi(h,+finger/set <field>=<text>)] - Set a finger field. Setting to nothing may clear it.%R[ansi(h,+finger/get <field>)] - Retrieves a field for editing. Can be used on other players as <player>/<field>)]%R%RFields settable by players: [u(itemize,u(game_config,FINGER,PLAYERSET),|,and,\,)]%RFields Only settable by Admin: [u(itemize,u(game_config,FINGER,ADMINSET),|,and,\,)]%R%RFields not listed here are controlled by other factors.[if(u(isadmin,%#),%R%R[u(subheader,Admin Section)]%RAdmin may set the fields of other characters using [ansi(h,+finger/set <name>/<field>=<text>)])]
+help/add Character/+finger=[u(cobj,finger)]/HLP`FINGER

&SHLP`FINGER [u(cobj,finger)]=[ansi(hc,Staff Configuration)]%R+finger is one of those commands that's difficult to make a generic version suited for all games. So this one can be customized easily using +gameconfig!%R%RFor FINGER, COLUMN1 and COLUMN2 use a |-delimited list of finger fields that can be customized with adding further options via ~ delimiters. The order for field delimiters is <GET>~<DISPLAY>~<DEFAULT>. As an example, if COLUMN1 is SEX~Gender~???|RACE~Species~Human, the RACE word will be used to GET DATA (using getproperty on the Master Code Object), it will display Species in the column, and if the GET returns nothing it will display Human by default.%R%RThe EXTRA option handles the bordered sections below the dual column section. It works similarly: GROUPNAMES2~Groups|SKILLS~Skills would show sections named Groups and Skills, with GETPROPERTY`GROUPNAMES2 being called for the first one.%R%RIf the system cannot find a GETPROPERTY entry for dynamic retrieval it will instead resort to searching for an attribute on the player: D`FINGER`<GET> such as D`FINGER`PROFILE. This is important because of PLAYERSET and ADMINSET, which allow you to set |-delimited WORDS. PLAYERSET values are entries players can set themselves while ADMINSET can only be changed by admin.%R%RHERE ARE SOME SPECIAL <GET>'s provided by Volund's Mushcode Suite:%RSEX: Retrieves @sex.%RNAME: Retrives name().%RMONIKER: Retrieves @moniker or cname() depending on game.%RALIASNAME: Shows name() then first alias() in parentheses.%RALIASMONIKER: Like above but uses @moniker.%RWIKI: Retrieves D`FINGER`WIKI and tries to clickify it.%RGROUPNAMES2: Shows clickable list of all Minor Group memberships.%RMAIL: Shows # Unread Messages.%RALIAS: Shows first @alias.%RLAST: Display idle or last-connect information.%REMAIL: Shows D`FINGER`EMAIL. If value begins with ! it is considered a hidden email and only admin see it.%RACCALTS: Display the same data that +alts would. Requires Account System.%R%RGROUP <GETS>:%RGroup data can be retrieved using some standardized formats. There are three <TYPES> for retrieving group information: MAJOR, MINOR, and FIRST. These attempt to retrieve data from the FIRST FOUND GROUP of the given type (with FIRST being ANY group but preferring Majors) and are identical. Only the LIST functions differ, creating data from ALL groups of the type. The ALL <TYPE> can also be used with LIST functions.%R<TYPE>NAME: Show the colored name of the group.%R<TYPE>NAMECLICK: Show a clickable <TYPE>NAME.%R<TYPE>ABBR: Show colored Abbreviation.%R<TYPE>LETTER: Show colored LETTER, derived from Abbreviation.%R<TYPE>RANK: Show character's rank in Faction-style.%R<TYPE>NAMELIST: Show comma-separated list of groups.%R<TYPE>NAMELISTLINE: Show group names separated by linebreaks.%R<TYPE>NAMELISTCLICK: Shows <TYPE>NAMELIST but clickable.%R<TYPE>NAMELISTLINECLICK: Show <TYPE>NAMELISTLINE but clickable.%R<TYPE>RANKLIST: Show faction-style ranks as comma-separated list.%R<TYPE>RANKLISTLINE: Show faction-style ranks as newline-separated list.%R%RYou can add additional GETPROPERTIES by adding them in the following format to the Master Code Object: GETPROPERTY`<GET>, such as GETPROPERTY`PROFILE. The person being checked is passed as \%0. Design these as a u()-function.%R%RA <GET> MUST be a single word. Alphanumeric characters, no spaces or special symbols!%R%RSome examples:%R+gameconfig finger/column1=FACTION~MAJORNAMECLICK~Unaffiliated|SEX~Gender|SPECIES~Species%R+gameconfig finger/column2=RANK~Rank~N/A|JOB~Job~Wanderer|TYPE~Character Type~N/A%R+gameconfig finger/extra=QUOTE~Quote|PROFILE~Profile|SKILLS~Skills|WIKI~Wiki%R%REXAMPLE of adding a new special <GET>:%R%R&GETPROPERTY`CODENAME [num(u(cobj,mco))]=get(\%0/CODENAME)%R(yes this is the correct dbref.)
+shelp/add Administration/+finger=[u(cobj,finger)]/SHLP`FINGER